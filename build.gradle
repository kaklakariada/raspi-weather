plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "2.0.2"
    id 'org.hidetake.ssh' version '2.9.0'
}

repositories {
    jcenter()
}

version = '0.0.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes 'Main-Class': 'weather.WeatherStation'
    }
}

tasks.withType(JavaCompile) {
	options.compilerArgs << '-Xlint:all'
	options.encoding = 'UTF-8'
}

dependencies {
    compile 'com.pi4j:pi4j-core:1.1'
    compile 'org.influxdb:influxdb-java:2.8'
    compile 'com.netflix.hystrix:hystrix-core:1.5.12'
    compile 'com.github.kaklakariada:fritzbox-java-api:0.4.1'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.13.0'
}

remotes {
  raspi {
    host = '192.168.179.68'
    user = 'pi'
    identity = file("${System.getProperty('user.home')}/.ssh/id_rsa")
  }
}

task deploy(dependsOn: shadowJar) {
  doLast {
    ssh.run {
      session(remotes.raspi) {
        def jarFile = shadowJar.outputs.files.singleFile
        def configFile = file('weather.properties')
        def uploadDir = '/home/pi/weather'
        execute "mkdir -p $uploadDir"
        put from: jarFile, into: uploadDir
        put from: configFile, into: uploadDir
        execute "cd $uploadDir && java -jar ${uploadDir}/${jarFile.name}"
      }
    }
  }
}

